plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'org.ethelred.mymailtool2.Main'
    applicationName = 'mymailtool2'
}

dependencies {
    implementation group: 'args4j', name: 'args4j', version: '2.0.8'
    implementation group: 'com.google.guava', name: 'guava', version: '11.0.2'
    implementation group: 'javax.activation', name: 'javax.activation-api', version: '1.2.0'
    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.5.0'
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.14'
    implementation group: 'org.mozilla', name: 'rhino', version: '1.7.14'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.jmock', name: 'jmock', version: '2.5.1'
    testImplementation group: 'org.jmock', name: 'jmock-legacy', version: '2.12.0'

}

task install {
    dependsOn installDist
    doLast {
        ant.mkdir(dir: "${rootDir}/bin")
        ant.symlink(resource: "${(installDist.outputs.files as List).first()}/bin/${application.applicationName}", link: "${rootDir}/bin/${application.applicationName}")
    }
}

task cleanBin(type: Delete) {
    delete 'bin'
}

clean.dependsOn cleanBin