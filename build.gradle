plugins {
    id 'application'
    id("org.openrewrite.rewrite") version("5.40.6")
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

application {
    mainClass = 'org.ethelred.mymailtool2.Main'
    applicationName = 'mymailtool2'
    applicationDefaultJvmArgs = ['-Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager']
}

dependencies {
    implementation group: 'args4j', name: 'args4j', version: '2.33'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'javax.activation', name: 'javax.activation-api', version: '1.2.0'
    implementation group: 'com.sun.mail', name: 'jakarta.mail', version: '2.0.1'
    implementation group: 'joda-time', name: 'joda-time', version: '2.12.5'
    implementation group: 'org.mozilla', name: 'rhino', version: '1.7.14'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    implementation 'org.ethelred:ethelred_util:0.4'
    implementation 'org.apache.logging.log4j:log4j-api:2.22.0'
    implementation 'org.apache.logging.log4j:log4j-jul:2.21.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.21.1'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.jmock', name: 'jmock', version: '2.12.0'
    testImplementation group: 'org.jmock', name: 'jmock-legacy', version: '2.12.0'

//    rewrite('org.ethelred:ethelred-rewrite:0.3-6-ga72db15.dirty')
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks:latest.release")
    rewrite("org.openrewrite.recipe:rewrite-migrate-java:1.21.1")
    rewrite('org.apache.logging.log4j:log4j-api:2.22.0')
}

task install {
    dependsOn installDist
    doLast {
        ant.mkdir(dir: "${rootDir}/bin")
        ant.symlink(resource: "${(installDist.outputs.files as List).first()}/bin/${application.applicationName}", link: "${rootDir}/bin/${application.applicationName}")
    }
}

task cleanBin(type: Delete) {
    delete 'bin'
}

clean.dependsOn cleanBin

rewrite {
    rewriteVersion = "7.30.0-SNAPSHOT"
//    activeRecipe('org.ethelred.rewrite.java.logging.jul.JulToLog4j2', "org.openrewrite.java.cleanup.Cleanup", "org.openrewrite.java.migrate.Java8toJava11", "org.openrewrite.java.cleanup.CommonStaticAnalysis")
    activeRecipe(
            "org.ethelred.PrintRecipe",
            "org.openrewrite.java.cleanup.Cleanup",
            "org.openrewrite.java.migrate.Java8toJava11",
            "org.openrewrite.java.cleanup.CommonStaticAnalysis"
    )
    exclusion('**/build.gradle')
}
